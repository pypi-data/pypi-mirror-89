#################################################
# This program does nothing useful.  It merely  #
# serves as a showcase of QMASM's features.     #
#                                               #
# By Scott Pakin <pakin@lanl.gov>               #
#################################################

# Weights
abc 0.5
def 0.5   # Comments are allowed on the same line as other statements.

# Strengths
abc def 0.5

# Assertions
!assert abc = 0 || def = 0

# Chains
abc = ghi

# Anti-chains
def /= jkl

# Let-binding to a symbol
!let yes := True

# Pins
jkl := yes

# Equivalences
jkl <-> mno

# Includes
!include "comparator"

# Assertions
!assert 1 + 2 + 3 = 6 || 4 + 5 * 6 = 4 + (5 * 6) && 4 + 5 * 6 < (4 + 5) * 6
!assert mno + 8 /= 3**(def + 1)
!assert 56^1 = (1<<4 | 1<<1 | 1<<!99)*(7&~4)
!assert 1 + 1 = 25 || 25/3 >= 25%7

# Macro definitions
!begin_macro XOR
!assert Y = A^B
  A    0.5
  B   -0.5
  Y   -0.5
  $a1  1

  A B   -0.5
  A Y   -0.5
  A $a1  1
  B Y    0.5
  B $a1 -1
  Y $a1 -1
!end_macro XOR

# Macro uses
!use_macro XOR excl_or
excl_or.A := true
excl_or.B := FaLsE   # Note: case-insensitive

# Let-binding to an expression
!let idx1 := (6 + 1)*2*(4 - 1)
!let idx2 := idx1/3
alfa[idx1] = alfa[idx2]

# Conditionals
!if idx1 < idx2
  alfa[idx1] = excl_or.Y
!else
  alfa[idx2] = excl_or.Y
!end_if

# Iteration over integer values
!for idx := 0, 2, ..., 15
  alfa[idx] = zulu
!end_for

# Iteration over symbols
!for icao := alfa, bravo, charlie, delta, echo
  icao[42] zulu -3.25
!end_for

# Changing the BQM type.
!bqm_type qubo
!begin_macro OR
A 1
B 1
Y 1
A B  1
A Y -2
B Y -2
!end_macro OR
!bqm_type ising
!use_macro OR qubo_or
!assert qubo_or.Y = qubo_or.A | qubo_or.B

# Chain of macros
!begin_macro XOR_chain
  !use_macro XOR xor
  A = xor.A
  B = xor.B
  Y = xor.Y
  Y = !next.A
!end_macro XOR_chain
!use_macro XOR_chain first second third
# (In this chain, first.Y = second.A and second.Y = third.A.)

# Renaming symbols
big       -0.75
small      0.75
big small  0.50
big   -> bigger
small -> smaller
six[2:0] := 110
!assert six[0] /= 1
six[2] = six[1]
six[1] /= six[0]
six[2:0] -> six[0:2]  # Convert from 110 to 011.
!assert six[2:0] = 3
